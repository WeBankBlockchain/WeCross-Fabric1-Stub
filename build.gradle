plugins {
    id 'com.github.sherter.google-java-format' version '0.8'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'maven-publish'
}

// Apply the java-library plugin to add support for Java Library
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
//apply plugin: 'war'
apply plugin: 'jacoco'
apply plugin: 'com.github.johnrengelman.shadow'
group 'com.webank.wecross'
version '1.0.0-rc4-07030-SNAPSHOT'

sourceCompatibility = 1.8

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url 'https://dl.bintray.com/ethereum/maven/' }
}

googleJavaFormat {
    options style: 'AOSP'
    source = sourceSets*.allJava
    include '**/*.java'
    exclude '**/temp/*.java'
}

verifyGoogleJavaFormat {
    source = sourceSets*.allJava
    include '**/*.java'
    exclude '**/temp/*.java'
}


dependencies {

    implementation 'com.moandjiezana.toml:toml4j:0.7.2'
    implementation 'org.slf4j:slf4j-api:1.7.25'

    // Fabric
    implementation 'javassist:javassist:3.12.1.GA'
    implementation "org.hyperledger.fabric-sdk-java:fabric-sdk-java:1.4.4"
    implementation 'io.netty:netty-tcnative-boringssl-static:2.0.27.Final'

    // WeCross
    implementation 'com.webank:wecross-java-stub:1.0.0-rc4-07010-SNAPSHOT'
    configurations.compile.exclude(group: 'ch.qos.logback')
    configurations.compile.exclude(group: 'org.apache.logging.log4j')

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

sourceSets {
    main {
        resources {
            exclude '**/*.toml'
            exclude '**/*.xml'
            exclude '**/*.properties'
            exclude '**/*.yml'
            exclude '**/*.crt'
            exclude '**/*.key'
            include 'chaincode/WeCrossProxy/proxy.go'
        }
    }
}

jar.enabled = false
project.tasks.assemble.dependsOn project.tasks.shadowJar

shadowJar {
    destinationDir file('dist/apps')
    archiveName project.name + "-" + project.version + '.jar'

    manifest {
        attributes(
                'Main-Class': 'com.webank.wecross.stub.fabric.FabricStubFactory',
                'version': project.version,
        )
    }

    doLast {
        copy {
            from file('src/main/resources/')
            into 'dist/conf'
        }
        copy {
            from file('.').listFiles().findAll { File f -> (f.name.endsWith('.sh') || f.name.endsWith('.env')) }
            into 'dist'
        }
        copy {
            from file('scripts/')
            into 'dist'
        }
    }

    // minimize() // Enable this leads to tcnative core

    // relocate 'io.netty', 'io.netty.fabric'
}


test {
    testLogging.showStandardStreams = true
    classpath = project.sourceSets.test.runtimeClasspath + files("${projectDir}/src/main/resources")

}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.webank'
            artifactId = 'wecross-fabric-stub'
            from components.java
            artifacts = [shadowJar]

            // https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
            pom {
                name = "WeCross-Fabric-Stub"
                description = "Fabric Stub for WeCross"
                url = "https://github.com/WeBankFinTech/WeCross"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "Shareong"
                        name = "Shareong"
                        email = "740310627@qq.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/WeBankFinTech/WeCross.git"
                    developerConnection = "scm:git:ssh://github.com/WeBankFinTech/WeCross.git"
                    url = "https://github.com/WeBankFinTech/WeCross"
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            //*
            def NEXUS_USERNAME = ''
            def NEXUS_PASSWORD = ''
            //*/
            credentials {
                username = NEXUS_USERNAME
                password = NEXUS_PASSWORD
            }

        }
    }
}
