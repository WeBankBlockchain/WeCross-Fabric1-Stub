plugins {
    id 'com.github.sherter.google-java-format' version '0.8'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

// Apply the java-library plugin to add support for Java Library
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
//apply plugin: 'war'
apply plugin: 'jacoco'
apply plugin: 'com.github.johnrengelman.shadow'
group 'com.webank.wecross'
version '1.0.0-rc2'

sourceCompatibility = 1.8

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url 'https://dl.bintray.com/ethereum/maven/' }
}

googleJavaFormat {
    options style: 'AOSP'
    source = sourceSets*.allJava
    include '**/*.java'
    exclude '**/temp/*.java'
}

verifyGoogleJavaFormat {
    source = sourceSets*.allJava
    include '**/*.java'
    exclude '**/temp/*.java'
}


dependencies {

    compile 'com.moandjiezana.toml:toml4j:0.7.2'
    compile 'org.springframework.boot:spring-boot-starter-web:2.1.4.RELEASE'
    compile 'org.springframework.boot:spring-boot-configuration-processor:2.1.4.RELEASE'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.slf4j:slf4j-api:1.7.25'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.1.4.RELEASE'

    // Fabric
    compile 'javassist:javassist:3.12.1.GA'
    compile "org.hyperledger.fabric-sdk-java:fabric-sdk-java:1.4.4"
    compile 'io.netty:netty-tcnative-boringssl-static:2.0.27.Final'

    // WeCross
    compile 'com.webank:wecross-java-stub:1.0.0-rc2-0408-SNAPSHOT'
    configurations.compile.exclude(group: 'ch.qos.logback')
    configurations.compile.exclude(group: 'org.apache.logging.log4j')
}

sourceSets {
    main {
        resources {
            exclude '/*'
        }
    }
}

jar.enabled = false
project.tasks.assemble.dependsOn project.tasks.shadowJar

shadowJar {
    destinationDir file('dist/apps')
    archiveName project.name + '.jar'

    manifest {
        attributes(
                'Main-Class': 'com.webank.wecross.FabricStub',
                'version': project.version,
        )
    }

    doLast {
        copy {
            from file('src/main/resources/')
            into 'dist/conf'
        }
        copy {
            from file('.').listFiles().findAll { File f -> (f.name.endsWith('.sh') || f.name.endsWith('.env')) }
            into 'dist'
        }
        copy {
            from file('scripts/')
            into 'dist'
        }
    }
}


test {
    testLogging.showStandardStreams = true
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

